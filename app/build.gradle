apply plugin: 'com.android.application'

/*
 * Copyright 2018 Kaushik N. Sanji
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Load the details for Application properties
def appPropertiesFile = rootProject.file("application.properties")
def appProperties = new Properties()
appProperties.load(new FileInputStream(appPropertiesFile))

// Load the details for signing the APK
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    // Constants defined for use with buildConfigField
    def STRING = "String"
    def GUARDIAN_API_KEY = "GUARDIAN_API_KEY"
    def GUARDIAN_API_KEY_VAL = '"' + appProperties['GUARDIAN_API_KEY_VAL'] + '"'

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.example.kaushiknsanji.novalines"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = "$applicationId-v$versionName"

        // Use the defined API KEY
        buildConfigField STRING, GUARDIAN_API_KEY, GUARDIAN_API_KEY_VAL
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //Support Library dependencies
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:preference-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:preference-v14:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.supportLibraryVersion"

    //ConstraintLayout Library dependency
    implementation "com.android.support.constraint:constraint-layout:$rootProject.ext.constraintLayoutVersion"

    //Test dependencies
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoCoreVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
